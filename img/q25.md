#include <Arduino.h>

#define TAILLE_MAX    5

QueueHandle_t xQueue;

void vSenderTask( void *pvParameters )
{
  const char *pcTaskName = "vSenderTask";
  int valueToSend;
  BaseType_t status;
  UBaseType_t uxPriority;

  valueToSend = ( int ) pvParameters;
  uxPriority = uxTaskPriorityGet( NULL );    

  for( ;; )
  {
    status = xQueueSendToBack( xQueue, &valueToSend, 0 );
    if( status == pdPASS )
    {
      Serial.printf("%s - envoyé = %d (priorite %d)\n", pcTaskName, valueToSend, uxPriority);
    }
    else
    {
      Serial.printf("%s - envoyé = aucun (priorite %d)\n", pcTaskName, uxPriority);
    }
  }
}

void vReceiverTask( void *pvParameters )
{
  const char *pcTaskName = "vReceiverTask";
  int receivedValue;
  BaseType_t status;
  UBaseType_t uxPriority;

  uxPriority = uxTaskPriorityGet( NULL );
  
  for( ;; )
  {
    Serial.printf("%s - nb elements = %d (priorite %d)\n", pcTaskName, uxQueueMessagesWaiting( xQueue ), uxPriority);
    status = xQueueReceive( xQueue, &receivedValue, pdMS_TO_TICKS( 100 ) );
    if( status == pdPASS )
    {
      Serial.printf("%s - lu = %d (priorite %d)\n", pcTaskName, receivedValue, uxPriority);
    }
    else
    {
      Serial.printf("%s - lu = aucun (priorite %d)\n", pcTaskName, uxPriority);
    }
  }
}

void setup()
{
  Serial.begin(115200);
  while (!Serial);
  Serial.println("Start");

  xQueue = xQueueCreate( TAILLE_MAX, sizeof( int ) );

  xTaskCreate( vSenderTask, "Sender1", 10000, (void *)100, 1, NULL );
  xTaskCreate( vSenderTask, "Sender2", 10000, (void *)200, 1, NULL );
  xTaskCreate( vReceiverTask, "Receiver", 10000, NULL, 2, NULL );

  for(;;); // pas de loop()
}

void loop()
{
}
Résultat :

...
vSenderTask - envoyé = 100 (priorite 1)
vReceiverTask - lu = 100 (priorite 2)
vReceiverTask - nb elements = 0 (priorite 2)
vSenderTask - envoyé = 100 (priorite 1)
vSenderTask - envoyé = 200 (priorite 1)
vReceiverTask - lu = 100 (priorite 2)
vReceiverTask - nb elements = 1 (priorite 2)
vReceiverTask - lu = 200 (priorite 2)
vReceiverTask - nb elements = 0 (priorite 2)
vSenderTask - envoyé = 200 (priorite 1)
vReceiverTask - lu = 200 (priorite 2)
vReceiverTask - nb elements = 0 (priorite 2)
vSenderTask - envoyé = 200 (priorite 1)
vReceiverTask - lu = 200 (priorite 2)
vReceiverTask - nb elements = 0 (priorite 2)
vSenderTask - envoyé = 200 (priorite 1)
vSenderTask - envoyé = 100 (priorite 1)
vReceiverTask - lu = 200 (priorite 2)
vReceiverTask - nb elements = 1 (priorite 2)
vReceiverTask - lu = 100 (priorite 2)
vReceiverTask - nb elements = 0 (priorite 2)
vSenderTask - envoyé = 100 (priorite 1)
vReceiverTask - lu = 100 (priorite 2)
vReceiverTask - nb elements = 0 (priorite 2)
...